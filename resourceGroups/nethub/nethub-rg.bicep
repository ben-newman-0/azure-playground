@minLength(1)
@maxLength(4)
param nameEnvironment string

@minLength(1)
@secure()
param vpnClientRootPublicCertData string

@minLength(6)
@maxLength(72)
@secure()
param virtualMachineDnsAdminPassword string

var virtualNetworkGatewayName = 'nethub-vgw-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'

resource virtualNetwork 'Microsoft.Network/virtualNetworks@2020-07-01' = {
  name: 'nethub-vnet-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
  location: 'northeurope'
  properties: {
    addressSpace: {
      addressPrefixes: [
        '10.1.0.0/16'
      ]
    }
    subnets: [
      {
        properties: {
          addressPrefix: '10.1.0.0/24'
        }
        name: 'nethub-snet-nethub-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
      }
      {
        properties: {
          addressPrefix: '10.1.1.0/24'
        }
        name: 'GatewaySubnet'
      }
    ]
  }
}

resource publicIPAddressVirtualNetworkGateway 'Microsoft.Network/publicIPAddresses@2020-07-01' = {
  name: 'nethub-pip-vgw-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
  location: 'northeurope'
  sku: {
    name: 'Basic'
    tier: 'Regional'
  }
  properties: {
    publicIPAllocationMethod: 'Dynamic' // Basic VGW SKU requires Dynamic
    publicIPAddressVersion: 'IPv4'
  }
}

resource virtualNetworkGateway 'Microsoft.Network/virtualNetworkGateways@2020-07-01' = {
  name: virtualNetworkGatewayName
  location: 'northeurope'
  properties: {
    ipConfigurations: [
      {
        properties: {
          privateIPAllocationMethod: 'Dynamic'
          publicIPAddress: {
            id: publicIPAddressVirtualNetworkGateway.id
          }
          subnet: {
            id: virtualNetwork.properties.subnets[1].id
          }
        }
        name: 'default'
      }
    ]
    gatewayType: 'Vpn'
    vpnType: 'RouteBased'
    vpnGatewayGeneration: 'Generation1'
    enableBgp: false
    sku: {
      name: 'Basic'
      tier: 'Basic'
    }
    vpnClientConfiguration: {
      vpnClientAddressPool: {
        addressPrefixes: [
          '10.2.0.0/24'
        ]
      }
      vpnClientRootCertificates: [
        {
          properties: {
            publicCertData: vpnClientRootPublicCertData
          }
          name: virtualNetworkGatewayName
        }
      ]
      vpnClientProtocols: [
        'SSTP'
      ]
    }
  }
}

resource networkInterfaceDns 'Microsoft.Network/networkInterfaces@2020-07-01' = {
  name: 'nethub-nic-dns-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
  location: 'northeurope'
  properties: {
    ipConfigurations: [
      {
        properties: {
          subnet: {
            id: virtualNetwork.properties.subnets[0].id
          }
        }
        name: 'nethub'
      }
    ]
  }
}

var virtualMachineDnsName = 'nethub-vm-dns-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
resource virtualMachineDns 'Microsoft.Compute/virtualMachines@2020-12-01' = {
  name: virtualMachineDnsName
  location: 'northeurope'
  properties: {
    hardwareProfile: {
      vmSize: 'Standard_B1s'
    }
    storageProfile: {
      imageReference: {
        // As of template creation 18.04 is only version that supports platform automatic patching
        // https://docs.microsoft.com/en-gb/azure/virtual-machines/automatic-vm-guest-patching#supported-os-images
        publisher: 'Canonical'
        offer: 'UbuntuServer'
        sku: '18.04-LTS'
        version: 'latest'
      }
      osDisk: {
        name: 'nethub-osdisk-dns-${nameEnvironment}-neu-${uniqueString(subscription().subscriptionId)}'
        createOption: 'FromImage'
        managedDisk: {
          storageAccountType: 'StandardSSD_LRS'
        }
      }
    }
    osProfile: {
      computerName: virtualMachineDnsName
      adminUsername: 'adm-dns'
      adminPassword: virtualMachineDnsAdminPassword
      linuxConfiguration: {
        patchSettings: {
          patchMode: 'AutomaticByPlatform'
        }
      }
    }
    networkProfile: {
      networkInterfaces: [
        {
          id: networkInterfaceDns.id
        }
      ]
    }
  }
  identity: {
    type: 'SystemAssigned' // Needed for AAD auth extension
  }

  resource virtualMachineDns_extensionAad 'extensions@2020-12-01' = {
    name: 'AADSSHLoginForLinux'
    location: 'northeurope'
    properties: {
      publisher: 'Microsoft.Azure.ActiveDirectory'
      type: 'AADSSHLoginForLinux'
      typeHandlerVersion: '1.0'
    }
  }

  resource virtualMachineDns_customScriptDnsConfig 'extensions@2020-12-01' = {
    name: 'dns-config'
    location: 'northeurope'
    properties: {
      publisher: 'Microsoft.Azure.Extensions'
      type: 'CustomScript'
      typeHandlerVersion: '2.1'
      settings: {
        // Script content generated by running 'cat ./resourceGroups/nethub/dns-config.sh | base64 -w0'
        script: 'ZWNobyAnSW5zdGFsbGluZyBiaW5kOS4uLicKYXB0LWdldCB1cGRhdGUgJiYgYXB0LWdldCBpbnN0YWxsIC15IGJpbmQ5CgplY2hvICdXcml0aW5nIGJpbmQgY29uZmlndXJhdGlvbiB0byAvZXRjL2JpbmQvbmFtZWQuY29uZi5vcHRpb25zLi4uJwpjYXQgPiAvZXRjL2JpbmQvbmFtZWQuY29uZi5vcHRpb25zIDw8IEVPRgpvcHRpb25zIHsKICBkaXJlY3RvcnkgIi92YXIvY2FjaGUvYmluZCI7CgogIHJlY3Vyc2lvbiB5ZXM7CiAgYWxsb3ctcXVlcnkgewogICAgMTAuMC4wLjAvODsKICAgIDE3Mi4xNi4wLjAvMTI7CiAgICAxOTIuMTY4LjAuMC8xNjsKICAgIDEyNy4wLjAuMTsKICB9OwoKICBmb3J3YXJkZXJzIHsgMTY4LjYzLjEyOS4xNjsgfTsKICBmb3J3YXJkIG9ubHk7Cn07CkVPRgoKZWNobyAnUmVzdGFydGluZyBiaW5kIHNlcnZpY2UuLi4nCnNlcnZpY2UgYmluZDkgcmVzdGFydAo='
      }
    }
  }
}

var privateLinkDnsZones = [
  'privatelink.azure-automation.net'
  'privatelink.database.windows.net'
  'privatelink.sql.azuresynapse.net'
  'privatelink.sqlondemand.azuresynapse.net'
  'privatelink.dev.azuresynapse.net'
  'privatelink.blob.core.windows.net'
  'privatelink.table.core.windows.net'
  'privatelink.queue.core.windows.net'
  'privatelink.file.core.windows.net'
  'privatelink.web.core.windows.net'
  'privatelink.dfs.core.windows.net'
  'privatelink.documents.azure.com'
  'privatelink.mongo.cosmos.azure.com'
  'privatelink.cassandra.cosmos.azure.com'
  'privatelink.gremlin.cosmos.azure.com'
  'privatelink.table.cosmos.azure.com'
  'privatelink.postgres.database.azure.com'
  'privatelink.mysql.database.azure.com'
  'privatelink.mariadb.database.azure.com'
  'privatelink.vaultcore.azure.net'
  'privatelink.search.windows.net'
  'privatelink.azurecr.io'
  'privatelink.azconfig.io'
  'privatelink.servicebus.windows.net'
  'privatelink.azure-devices.net'
  'privatelink.eventgrid.azure.net'
  'privatelink.azurewebsites.net'
  'privatelink.api.azureml.ms'
  'privatelink.notebooks.azure.net'
  'privatelink.service.signalr.net'
  'privatelink.monitor.azure.com'
  'privatelink.oms.opinsights.azure.com'
  'privatelink.ods.opinsights.azure.com'
  'privatelink.agentsvc.azure-automation.net'
  'privatelink.cognitiveservices.azure.com'
  'privatelink.afs.azure.net'
  'privatelink.datafactory.azure.net'
  'privatelink.adf.azure.com'
  'privatelink.redis.cache.windows.net'
  'privatelink.redisenterprise.cache.azure.net'
]

resource privateDnsZones 'Microsoft.Network/privateDnsZones@2020-06-01' = [for privateLinkDnsZone in privateLinkDnsZones: {
  name: privateLinkDnsZone
  location: 'global'
  properties: {}
}]

resource privateDnsZone_virtualNetworkLink 'Microsoft.Network/privateDnsZones/virtualNetworkLinks@2020-06-01' = [for privateLinkDnsZone in privateLinkDnsZones: {
  name: '${privateLinkDnsZone}/${virtualNetwork.name}'
  dependsOn: [
    privateDnsZones
  ]
  location: 'global'
  properties: {
    virtualNetwork: {
      id: virtualNetwork.id
    }
    registrationEnabled: false
  }
}]
